plugins {
    id 'java-library'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0' // Updated version
}

group 'ch.unibas.dmi.dbis'
version '0.0.1-ALPHA'

application {
    mainClass = 'ch.unibas.dmi.dbis.cs108.Main'
    // Add JavaFX modules to runtime
    applicationDefaultJvmArgs = ['--module-path', "${System.getProperty('java.home')}/lib/",
                                 '--add-modules', 'javafx.controls,javafx.fxml,javafx.base']
}

javadoc {
    source = sourceSets.main.allJava
    destinationDir = layout.buildDirectory.dir("docs/Documentation/javadoc").get().asFile
    options.encoding = "UTF-8"
}

javafx {
    version = "19.0.2.1"
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.base']
}

sourceSets {
    main {
        java.srcDirs = ['src/main/java']
        resources.srcDirs = ['src/main/resources']
    }
    client {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
        compileClasspath += sourceSets.main.compileClasspath
        runtimeClasspath += sourceSets.main.runtimeClasspath
    }
    server {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
        compileClasspath += sourceSets.main.compileClasspath
        runtimeClasspath += sourceSets.main.runtimeClasspath
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.2')
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'

    testImplementation 'org.mockito:mockito-core:5.5.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.5.0'

    implementation 'com.google.code.gson:gson:2.10.1'

    implementation "org.openjfx:javafx-base:${javafx.version}"
    implementation "org.openjfx:javafx-controls:${javafx.version}"
    implementation "org.openjfx:javafx-fxml:${javafx.version}"
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
        events "failed", "skipped", "passed", "standardOut", "standardError", "started"
    }
    systemProperty "file.encoding", "utf-8"
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

tasks.withType(Test).configureEach {
    systemProperty "file.encoding", "utf-8"
}

tasks.withType(JavaExec).configureEach {
    systemProperty "file.encoding", "utf-8"
}

jar {
    archiveBaseName = 'settlersOfAsgard'
    archiveVersion = ''
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    manifest {
        attributes 'Main-Class': application.mainClass
    }
    from {
        configurations.runtimeClasspath.collect { file -> file.isDirectory() ? file : zipTree(file)
        }
    }
}

tasks.register('clientJar', Jar) {
    archiveBaseName = 'settlersOfAsgard'
    archiveVersion = ''
    archiveClassifier = 'client'
    from sourceSets.client.output
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    manifest {
        attributes 'Main-Class': 'ch.unibas.dmi.dbis.cs108.client.app.Main'
    }
    from {
        configurations.runtimeClasspath.collect { file -> file.isDirectory() ? file : zipTree(file)
        }
    }
}

tasks.register('serverJar', Jar) {
    archiveBaseName = 'settlersOfAsgard'
    archiveVersion = ''
    archiveClassifier = 'server'
    from sourceSets.server.output
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    manifest {
        attributes 'Main-Class': 'ch.unibas.dmi.dbis.cs108.server.Main'
    }
    from {
        configurations.runtimeClasspath.collect { file -> file.isDirectory() ? file : zipTree(file)
        }
    }
}

tasks.register('build-cs108') {
    dependsOn jar, clientJar, serverJar, javadoc
    description = 'Builds all JAR files and Javadocs'
    group = 'build'
}

build.dependsOn clientJar, serverJar

// Add specific run tasks for client and server
tasks.register('runClient', JavaExec) {
    group = 'application'
    description = 'Runs the client application'
    mainClass = 'ch.unibas.dmi.dbis.cs108.client.app.ClientMainApp'
    classpath = sourceSets.client.runtimeClasspath + sourceSets.main.runtimeClasspath
    systemProperty "file.encoding", "utf-8"

    jvmArgs = [
        '--module-path', classpath.asPath,
        '--add-modules', 'javafx.controls,javafx.fxml,javafx.base'
    ]
}

tasks.register('runServer', JavaExec) {
    group = 'application'
    description = 'Runs the server application'
    mainClass = 'ch.unibas.dmi.dbis.cs108.server.Main'
    classpath = sourceSets.server.runtimeClasspath + sourceSets.main.runtimeClasspath
    systemProperty "file.encoding", "utf-8"
}